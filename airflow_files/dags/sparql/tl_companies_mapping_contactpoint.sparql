PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX org:        <http://www.w3.org/ns/org#>
PREFIX meemoo:     <https://data.hetarchief.be/ns/organisatie/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <https://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX graphs: <https://data.hetarchief.be/graph/>
PREFIX source: <https://data.hetarchief.be/ns/source/>

WITH graphs:organizations
INSERT {
    ?org schema:contactPoint ?contact, ?contact_ontsluiting, ?contact_facturatie;
         meemoo:requestForm ?aanvraagformulier.

    ?contact a schema:ContactPoint;
            schema:contactType ?etype;
            schema:email ?email;
            schema:telephone ?telnumber.
    
   ?contact_ontsluiting a schema:ContactPoint;
            schema:contactType "ontsluiting";
            schema:email ?email_ontsluiting;
            schema:telephone ?telnumber_ontsluiting.     

    ?contact_facturatie a schema:ContactPoint;
            schema:contactType "facturatie";
            schema:email ?email_facturatie.        
}
USING graphs:tl_companies
USING graphs:tl_custom_fields
WHERE {
    BIND ('https://data.hetarchief.be/id/organisatie/' AS ?ns)
    # Organizations
    ?cf_orid source:id ?cf_orid_id; source:label "5.1 - OR-ID" . 
    ?o source:custom_fields [
        source:value ?orid;
        source:definition [
            #source:id "c3a10038-7a8e-0e96-bd4a-53e4668e6244"
            source:id ?cf_orid_id
        ]
    ].
    
    OPTIONAL {
       ?o source:emails ?emails.
       ?emails source:type ?etype;
               source:email ?email

        OPTIONAL {
            ?o source:telephones ?telephones.
            ?telephones source:type ?etype;
            source:number ?telnumber
        }
    }
    

    BIND (URI(CONCAT(?ns, ?orid)) AS ?org)
    BIND (URI(CONCAT(?ns, MD5(CONCAT(?orid, '-', ?etype)))) AS ?contact)  
    
    BIND (URI(CONCAT(?ns, MD5(CONCAT(?orid, '-ontsluiting')))) AS ?contact_ontsluiting)
    BIND (URI(CONCAT(?ns, MD5(CONCAT(?orid, '-facturatie')))) AS ?contact_facturatie)   

    # E-mail Ontsluiting
    OPTIONAL {
        ?cf_email_ontsluiting source:id ?cf_email_ontsluiting_id; source:label "E-mail Ontsluiting" .
        ?o source:custom_fields [
            source:value ?email_ontsluiting;
            source:definition [
                #source:id "1b8878e5-4aed-0aa7-8055-63b8f8165afa"
                source:id ?cf_email_ontsluiting_id
            ]
        ]
    }
    # Telefoon Ontsluiting
    OPTIONAL {
        ?cf_tel_ontsluiting source:id ?cf_tel_ontsluiting_id; source:label "Telefoon Ontsluiting" .
        ?o source:custom_fields [
            source:value ?telnumber_ontsluiting;
            source:definition [
                #source:id "bbe4eb80-7f68-022e-b856-60c3e2665af9"
                source:id ?cf_tel_ontsluiting_id
            ]
        ]
    }

    # E-mail Facturatie
    OPTIONAL {
        ?cf_email_facturatie source:id ?cf_email_facturatie_id; source:label "E-mail Facturatie" .
        ?o source:custom_fields [
            source:value ?email_facturatie;
            source:definition [
                #source:id "1b8878e5-4aed-0aa7-8055-63b8f8165afa"
                source:id ?cf_email_facturatie_id
            ]
        ]
    }

    # Link aanvraagformulier
    OPTIONAL {
        ?cf_form source:id ?cf_form_id; source:label "CP - Link aanvraagformulier" .
        ?o source:custom_fields [
            source:value ?aanvraagformulier;
            source:definition [
                #source:id "e02a506d-7457-01e1-b052-4ae35b565afb"
                source:id ?cf_form_id
            ]
        ]
    }
}