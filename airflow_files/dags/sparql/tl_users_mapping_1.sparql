PREFIX foaf:        <http://xmlns.com/foaf/0.1/>
PREFIX org:        <http://www.w3.org/ns/org#>
PREFIX meemoo:     <https://meemoo.be/ns/meemoo#>
PREFIX skos:        <http://www.w3.org/2004/02/skos/core#>
PREFIX schema:      <https://schema.org/>
PREFIX xsd:         <http://www.w3.org/2001/XMLSchema#>

PREFIX graphs: <https://data.meemoo.be/graphs/>
PREFIX source: <https://data.meemoo.be/sources/>

#WITH graphs:organizations
INSERT {
    GRAPH graphs:organizations {
        ?am a schema:Person, foaf:Agent; 
            schema:givenName ?fn; 
            schema:familyName ?ln;
            schema:email ?mbox;
            org:holds ?post.

        ?post  a org:Post;
                org:postIn <http://data.meemoo.be/organizations/OR-w66976m>; #meemoo
                org:role ?role.

        ?role a org:Role; skos:prefLabel ?function_label. 
    }
}
USING graphs:tl_users
WHERE {
    # Account manager
    ?ru source:id ?amid;
        source:first_name ?fn;
        source:last_name ?ln;
        source:email ?email .
        
    OPTIONAL { 
        ?ru source:function ?function.
        BIND (STRLANG(?function, "nl") AS ?function_label)
        BIND (URI(CONCAT('https://data.meemoo.be/posts/', ENCODE_FOR_URI(?function))) AS ?post)
        BIND (URI(CONCAT('https://data.meemoo.be/roles/', ENCODE_FOR_URI(?function))) AS ?role)
    }

    BIND (URI(CONCAT('https://data.meemoo.be/people/', ?amid)) AS ?am)
    BIND (CONCAT('mailto:', ?email) AS ?mbox)
}