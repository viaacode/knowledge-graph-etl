PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX org:        <http://www.w3.org/ns/org#>
PREFIX meemoo:     <https://meemoo.be/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <https://schema.org/>
PREFIX map: <https://meemoo.be/ns/sources#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {

    ?org a org:Organization;
            skos:altLabel ?orgName;
            meemoo:accountManager ?am;
            foaf:homepage ?homepage;
            org:hasUnit ?unit;
            org:hasPrimarySite ?site.
            
    ?site  a org:Site;
           org:siteAddress ?siteAddress.
    
    ?siteAddress a schema:PostalAddress;
        schema:streetAddress ?streetAddress;
        schema:addressCountry "BE"^^xsd:string;
        schema:postalCode ?postalCode;
        schema:addressRegion ?region;
        schema:addressLocality ?city.

    ?unit a org:OrganizationalUnit;
        skos:prefLabel ?uname.

    ?u_site a org:Site;
           org:siteAddress ?u_siteAddress.
    
    ?u_siteAddress a schema:PostalAddress;
        schema:streetAddress ?u_streetAddress;
        schema:addressCountry "BE"^^xsd:string;
        schema:postalCode ?u_postalCode;
        schema:addressRegion ?u_region;
        schema:addressLocality ?u_city.
        
}
WHERE {
    # Organization
    ?o map:objectclass "organization";
    map:o ?orid;
    map:description ?orgName .

    BIND (URI(CONCAT('http://data.meemoo.be/organizations/', ?orid)) AS ?org)

    # Address
    ?o map:street ?streetAddress;
        map:postalcode ?postalCode;
        map:l ?city.
    
    OPTIONAL {
        ?o map:st ?region.
    }
    BIND (URI(CONCAT('http://data.meemoo.be/sites/', ?orid)) AS ?site)        
    BIND (URI(CONCAT('http://data.meemoo.be/addresses/', MD5(?streetAddress))) AS ?siteAddress)

    # Units
    OPTIONAL {
        ?o map:units ?u.

        ?u map:ou ?ou;
           map:description ?uname .

        BIND (URI(CONCAT('http://data.meemoo.be/organizations/', ?ou)) AS ?unit)


        # Address
        ?u map:street ?u_streetAddress;
        map:postalcode ?u_postalCode;
        map:l ?u_city.

        OPTIONAL {
            ?u map:st ?u_region.
        }

        BIND (URI(CONCAT('http://data.meemoo.be/sites/', ?ou)) AS ?u_site)        
        BIND (URI(CONCAT('http://data.meemoo.be/addresses/', MD5(?u_streetAddress))) AS ?u_siteAddress)

    }
}