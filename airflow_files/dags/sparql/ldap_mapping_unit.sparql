PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX org:        <http://www.w3.org/ns/org#>
PREFIX meemoo:     <https://data.hetarchief.be/ns/organisatie#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <https://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX graphs: <https://data.hetarchief.be/graph/>
PREFIX source: <https://data.hetarchief.be/ns/source#>

WITH graphs:organizations
INSERT {
        ?org org:hasUnit ?unit.

        ?unit a org:OrganizationalUnit;
            org:unitOf ?org;
            schema:identifier ?ou;
            skos:prefLabel ?name;

            meemoo:sector ?sector;
            org:hasPrimarySite ?site;
            org:hasSite ?site.

        ?site  a org:Site;
            org:siteAddress ?siteAddress.
        
        ?siteAddress a schema:PostalAddress;
            schema:streetAddress ?streetAddress;
            schema:addressCountry "BE"^^xsd:string;
            schema:postalCode ?postalCode;
            schema:addressRegion ?region;
            schema:addressLocality ?city. 
    
}
USING graphs:ldap_organizations
WHERE {
    BIND ('https://data.hetarchief.be/id/organisatie/' AS ?ns)

    # Units
    ?u source:objectClass "organizationalUnit";
        source:ou ?ou;
        source:description ?name .
    
    # Organization
    ?units source:attributes ?u.
    ?x source:units ?units;
       source:attributes ?o.
       
    ?o source:objectClass "organization";
       source:o ?orid

    BIND (URI(CONCAT(?ns, ?orid)) AS ?org)

    # Address
    OPTIONAL {
        ?u source:street ?streetAddress
    }
    OPTIONAL {
        ?u source:postalCode ?postalCode;
    }
    OPTIONAL {
        ?u source:l ?city.
    }
    OPTIONAL {
        ?u source:st ?region.
    }

    OPTIONAL {
        ?u source:x-be-viaa-sector ?sector.
    }
    
    BIND (URI(CONCAT(?ns, MD5(?ou))) AS ?unit)
    BIND (URI(CONCAT(?ns, MD5(CONCAT(?ou, '-site')))) AS ?site)        
    BIND (URI(CONCAT(?ns, MD5(?streetAddress))) AS ?siteAddress)
}