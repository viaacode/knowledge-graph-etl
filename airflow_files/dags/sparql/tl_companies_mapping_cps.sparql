PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX org:        <http://www.w3.org/ns/org#>
PREFIX meemoo:     <https://data.hetarchief.be/ns/organisatie#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <https://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX graphs: <https://data.hetarchief.be/graph/>
PREFIX source: <https://data.hetarchief.be/ns/source/>

WITH graphs:organizations
INSERT {
        ?org a meemoo:ContentPartner.
}
USING graphs:tl_companies
USING graphs:tl_custom_fields
WHERE {
    # Organizations
    ?cf_orid source:id ?cf_orid_id; source:label "5.1 - OR-ID" . 
    ?cf_status source:id ?cf_status_id; source:label "CP Status" . 
    ?o source:custom_fields [
        source:value ?orid;
        source:definition [
            #source:id "c3a10038-7a8e-0e96-bd4a-53e4668e6244"
            source:id ?cf_orid_id
        ]
    ],
    # CP Status
    [
        source:value "ja";
        source:definition [
            #source:id "f5c153c5-3c96-07e7-994d-cc70e1436247"
            source:id ?cf_status_id
        ]
    ] .

    BIND (URI(CONCAT('https://data.hetarchief.be/id/organisatie/', ?orid)) AS ?org)

}