PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX org:        <http://www.w3.org/ns/org#>
PREFIX meemoo:     <https://data.hetarchief.be/ns/organisatie#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <https://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dct: <http://purl.org/dc/terms/>

PREFIX graphs: <https://data.hetarchief.be/graph/>
PREFIX source: <https://data.hetarchief.be/ns/source#>

WITH graphs:organizations
INSERT {
        ?org a org:Organization;
                schema:identifier ?orid;
                skos:prefLabel ?orgName;
                foaf:homepage ?homepage;
                schema:logo ?logo;
                dct:description ?desc;
                org:hasPrimarySite ?site;
                org:hasSite ?site.
                
        ?site a org:Site;
            org:siteAddress ?siteAddress.
        
        ?siteAddress a schema:PostalAddress;
            schema:streetAddress ?streetAddress;
            schema:addressCountry ?country;
            schema:postalCode ?postalCode;
            schema:addressLocality ?city.
            
        ?org meemoo:hasAccountManager ?am.
        ?am meemoo:isAccountManagerOf ?org.
}
USING graphs:tl_companies
USING graphs:tl_custom_fields
WHERE {
    BIND ('https://data.hetarchief.be/id/organisatie/' AS ?ns)
    # Organizations
    ?cf_orid source:id ?cf_orid_id; source:label "5.1 - OR-ID" . 
    ?o source:custom_fields [
        source:value ?orid;
        source:definition [
            #source:id "c3a10038-7a8e-0e96-bd4a-53e4668e6244"
            source:id ?cf_orid_id
        ]
    ] .
    BIND (URI(CONCAT(?ns, MD5(CONCAT(?orid,'-primary')))) AS ?site)
    BIND (URI(CONCAT(?ns, ?orid)) AS ?org)
    BIND (URI(CONCAT('https://assets.viaa.be/images/', ?orid)) AS ?logo)

    OPTIONAL { ?o source:name ?orgName. }
    OPTIONAL { 
        ?o source:website ?website.

        BIND (URI(IF(STRSTARTS(?website, 'www'),CONCAT('http://',?website),?website)) AS ?homepage)
    }

    # Address
    OPTIONAL {
        ?o source:addresses ?addresses.

        ?addresses source:type "primary"; 
                   source:address ?primaryAddress.

        ?primaryAddress source:line_1 ?streetAddress;
                source:postal_code ?postalCode;
                source:city ?city;
                source:country ?country. 

        BIND (URI(CONCAT(?ns, MD5(CONCAT(?streetAddress, ?postalCode, ?city)))) AS ?siteAddress)
    }

    # CP - Omschrijving (200 tek.)
    OPTIONAL {
        ?cf_omsch source:id ?cf_omsch_id; source:label "CP - Omschrijving (200 tek.)" .
        ?o source:custom_fields [
            source:value ?omschrijving_field;
            source:definition [
                source:id ?cf_omsch_id #"664d253e-0e54-0c94-b455-02ae8e265afc"
            ]
        ]
        BIND (STRLANG(?omschrijving_field, "nl") AS ?desc)
    }

    # Accountmanagers
    OPTIONAL {
        ?o source:responsible_user [
            source:id ?amid
        ].

        BIND (URI(CONCAT(?ns, MD5(?amid))) AS ?am) 
    }
}