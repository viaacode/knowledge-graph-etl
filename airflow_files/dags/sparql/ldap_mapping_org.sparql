PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX org:        <http://www.w3.org/ns/org#>
PREFIX meemoo:     <https://data.hetarchief.be/ns/organisatie#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <https://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX graphs: <https://data.hetarchief.be/graph/>
PREFIX source: <https://data.hetarchief.be/ns/source#>

WITH graphs:organizations
INSERT {
        ?org a org:Organization;
                schema:identifier ?orid;
                skos:altLabel ?orgName;
                meemoo:sector ?sector;
                org:hasPrimarySite ?site;
                org:hasSite ?site.
                
        ?site  a org:Site;
            org:siteAddress ?siteAddress.
        
        ?siteAddress a schema:PostalAddress;
            schema:streetAddress ?streetAddress;
            schema:addressCountry "BE"^^xsd:string;
            schema:postalCode ?postalCode;
            schema:addressRegion ?region;
            schema:addressLocality ?city. 
}
USING graphs:ldap_organizations
WHERE {
    BIND ('https://data.hetarchief.be/id/organisatie/' AS ?ns)
    # Organization
    ?o source:objectClass "organization";
        source:o ?orid;
        source:description ?orgName .

    BIND (URI(CONCAT(?ns, ?orid)) AS ?org)

    # Address
    OPTIONAL {
        ?o source:street ?streetAddress
    }
    OPTIONAL {
        ?o source:postalCode ?postalCode;
    }
    OPTIONAL {
        ?o source:l ?city.
    }
    OPTIONAL {
        ?o source:st ?region.
    }

    OPTIONAL {
        ?o source:x-be-viaa-sector ?sector.
    }

    BIND (URI(CONCAT(?ns, MD5(CONCAT(?orid, '-primary')))) AS ?site)        
    BIND (URI(CONCAT(?ns, MD5(CONCAT(?streetAddress, ?postalCode, ?city)))) AS ?siteAddress)
}