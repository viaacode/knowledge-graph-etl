PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX org:        <http://www.w3.org/ns/org#>
PREFIX meemoo:     <https://meemoo.be/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <https://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX graphs: <https://data.meemoo.be/graphs/>
PREFIX source: <https://data.meemoo.be/sources/>

WITH graphs:organizations
INSERT {
    ?org schema:contactPoint ?contact.
    ?contact a schema:ContactPoint;
            schema:contactType ?etype;
            schema:email ?email;
            schema:telephone ?telnumber.
}
USING graphs:tl_companies
WHERE {
    # Organizations
    ?o source:custom_fields [
        source:value ?orid;
        source:definition [
            source:id "c3a10038-7a8e-0e96-bd4a-53e4668e6244"
        ]
    ];
    source:emails [
        source:type ?etype;
        source:email ?email
    ];
    source:telephones [
        source:type ?ttype;
        source:number ?telnumber
    ].
    BIND (URI(CONCAT('https://data.meemoo.be/organizations/', ?orid)) AS ?org)
    BIND (URI(CONCAT('https://data.meemoo.be/organizations/', ?orid, '-', ?etype)) AS ?contact)   
}