PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX org:        <http://www.w3.org/ns/org#>
PREFIX meemoo:     <https://data.hetarchief.be/ns/organisatie/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <https://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX graphs: <https://data.hetarchief.be/graph/>
PREFIX source: <https://data.hetarchief.be/ns/source/>

WITH graphs:organizations
INSERT {
        ?org meemoo:allowsOverlay ?overlay;
             meemoo:allowsBZT ?bzt
}
USING graphs:tl_companies
USING graphs:tl_custom_fields
WHERE {
    # Organizations
    ?cf_orid source:id ?cf_orid_id; source:label "5.1 - OR-ID" . 
    ?cf_overlay source:id ?cf_overlay_id; source:label "4.2 - Watermerk" .
    ?cf_bzt source:id ?cf_bzt_id; source:label "4.1 - Bezoekertool" .

    ?o source:custom_fields [
        source:value ?orid;
        source:definition [
            source:id ?cf_orid_id #"c3a10038-7a8e-0e96-bd4a-53e4668e6244"
        ]
    ],
    # 4.2 - Overlay
    [
        #"true"^^<http://www.w3.org/2001/XMLSchema#boolean>
        source:value ?overlay;
        source:definition [
            source:id ?cf_overlay_id # "9987d9fa-0230-0205-b75e-6cd219362f46"
        ]
    ],
    # 4.1 - Bezoekertool
    [
        #"true"^^<http://www.w3.org/2001/XMLSchema#boolean>
        source:value ?bzt;
        source:definition [
            source:id ?cf_bzt_id #"efc39617-3241-0564-a350-71dfa9f66914"
        ]
    ] .

    BIND (URI(CONCAT('https://data.hetarchief.be/id/organisatie/', ?orid)) AS ?org)

}