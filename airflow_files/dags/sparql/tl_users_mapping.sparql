PREFIX foaf:        <http://xmlns.com/foaf/0.1/>
PREFIX org:        <http://www.w3.org/ns/org#>
PREFIX skos:        <http://www.w3.org/2004/02/skos/core#>
PREFIX schema:      <https://schema.org/>
PREFIX xsd:         <http://www.w3.org/2001/XMLSchema#>

PREFIX graphs: <https://data.hetarchief.be/graph/>
PREFIX source: <https://data.hetarchief.be/ns/source/>

#WITH graphs:organizations
INSERT {
    GRAPH graphs:organizations {
        ?am a schema:Person, foaf:Agent; 
            schema:givenName ?fn; 
            schema:familyName ?ln;
            schema:email ?mbox;
            schema:telephone ?tel_nr;
            org:holds ?post.

        ?post  a org:Post;
                org:postIn <https://data.hetarchief.be/id/organization/OR-w66976m>; #meemoo
                org:role ?role.

        ?role a org:Role; skos:prefLabel ?function_label. 
    }
}
USING graphs:tl_users
WHERE {
    BIND ('https://data.hetarchief.be/id/organization/' AS ?ns)
    # Account manager
    ?ru source:id ?amid;
        source:first_name ?fn;
        source:last_name ?ln;
        source:email ?email.

    OPTIONAL {
        ?ru source:telephones ?tel.
        ?tel source:type ?tel_type;
            source:number ?tel_nr.
    }
        
    OPTIONAL { 
        ?ru source:function ?function.
    }
    BIND (STRLANG(?function, "nl") AS ?function_label)
    BIND (URI(CONCAT(?ns, MD5(CONCAT(?amid,?function)))) AS ?post)
    BIND (URI(CONCAT('https://data.hetarchief.be/id/organisatie-functie#', ENCODE_FOR_URI(?function))) AS ?role)

    BIND (URI(CONCAT(?ns, MD5(?amid))) AS ?am)
    BIND (CONCAT('mailto:', ?email) AS ?mbox)
}