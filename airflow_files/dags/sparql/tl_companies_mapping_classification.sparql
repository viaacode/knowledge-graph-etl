PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX org:        <http://www.w3.org/ns/org#>
PREFIX meemoo:     <https://meemoo.be/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <https://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX graphs: <https://data.meemoo.be/graphs/>
PREFIX source: <https://data.meemoo.be/sources/>

WITH graphs:organizations
INSERT {
        ?org org:classification ?class.
}
USING graphs:tl_companies
WHERE {
    ?o source:custom_fields [
        source:value ?orid;
        source:definition [
            source:id "c3a10038-7a8e-0e96-bd4a-53e4668e6244"
        ]
    ].
    # Organizations
    OPTIONAL {
        ?o source:custom_fields [
            source:value ?class_field;
            source:definition [
                source:id "ce9d448c-6cd5-09b5-af49-54d73c946242"
            ]
        ].
        BIND (URI(CONCAT('https://data.meemoo.be/terms/org/types#', LCASE(REPLACE(STRAFTER(?class_field, ' - '),' ','_')))) AS ?class)
    }

    BIND (URI(CONCAT('https://data.meemoo.be/organizations/', ?orid)) AS ?org)
}