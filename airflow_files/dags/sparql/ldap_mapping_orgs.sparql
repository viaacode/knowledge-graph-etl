PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX org:        <http://www.w3.org/ns/org#>
PREFIX meemoo:     <https://meemoo.be/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX schema: <https://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

PREFIX graphs: <https://data.meemoo.be/graphs/>
PREFIX source: <https://data.meemoo.be/sources/>

#WITH graphs:organizations
INSERT {
    GRAPH graphs:organizations {
        ?org a org:Organization;
                skos:altLabel ?orgName;
                foaf:homepage ?homepage;
                org:hasUnit ?unit;
                org:hasPrimarySite ?site.
                
        ?site  a org:Site;
            org:siteAddress ?siteAddress.
        
        ?siteAddress a schema:PostalAddress;
            schema:streetAddress ?streetAddress;
            schema:addressCountry "BE"^^xsd:string;
            schema:postalCode ?postalCode;
            schema:addressRegion ?region;
            schema:addressLocality ?city.

        ?unit a org:OrganizationalUnit;
            skos:prefLabel ?uname.

        ?u_site a org:Site;
            org:siteAddress ?u_siteAddress.
        
        ?u_siteAddress a schema:PostalAddress;
            schema:streetAddress ?u_streetAddress;
            schema:postalCode ?u_postalCode;
            schema:addressRegion ?u_region;
            schema:addressLocality ?u_city.
    }
}
USING graphs:ldap_organizations
WHERE {
    # Organization
    ?o source:objectClass "organization";
    source:o ?orid;
    source:description ?orgName .

    BIND (URI(CONCAT('http://data.meemoo.be/organizations/', ?orid)) AS ?org)

    # Address
    ?o source:street ?streetAddress;
        source:postalCode ?postalCode;
        source:l ?city.

    OPTIONAL {
        ?o source:st ?region.
    }
    BIND (URI(CONCAT('https://data.meemoo.be/sites/', ?orid, '-primary')) AS ?site)        
    BIND (URI(CONCAT('https://data.meemoo.be/addresses/', MD5(CONCAT(?streetAddress, ?postalCode, ?city)))) AS ?siteAddress)

    # Units
    OPTIONAL {
        ?o source:units ?u.

        ?u source:ou ?ou;
           source:description ?uname .

        BIND (URI(CONCAT('https://data.meemoo.be/organizations/', ?ou)) AS ?unit)


        # Address
        ?u source:street ?u_streetAddress;
        source:postalCode ?u_postalCode;
        source:l ?u_city.

        OPTIONAL {
            ?u source:st ?u_region.
        }

        BIND (URI(CONCAT('https://data.meemoo.be/sites/', ?ou)) AS ?u_site)        
        BIND (URI(CONCAT('https://data.meemoo.be/addresses/', MD5(?u_streetAddress))) AS ?u_siteAddress)

    }
}